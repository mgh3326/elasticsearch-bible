buildscript {
    ext {
        pluginName = 'detect-and-alarm'
        pluginClassname = 'kr.co.wikibook.DetectAndAlarmPlugin'
        pluginDescription = 'simple AccessControlPlugin example'
        elasticsearchVersion = project.hasProperty('esVer')
                ? project.getProperties().get('esVer') : '8.4.2'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30"
        classpath "org.elasticsearch.gradle:build-tools:${elasticsearchVersion}"
    }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'elasticsearch.esplugin'

group 'kr.co.wikibook'
version '1.0.0.0' + '-es-' + elasticsearchVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.elasticsearch:elasticsearch:${elasticsearchVersion}"

    implementation "com.github.kittinunf.fuel:fuel:2.3.1"
}

esplugin {
    name pluginName
    description pluginDescription
    classname pluginClassname
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}