buildscript {
    ext {
        pluginName = 'action-filter'
        pluginClassname = 'kr.co.wikibook.ActionFilterPlugin'
        pluginDescription = 'simple AccessControlPlugin example'
        elasticsearchVersion = project.hasProperty('esVer')
                ? project.getProperties().get('esVer') : '8.4.2'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30"
        classpath "org.elasticsearch.gradle:build-tools:${elasticsearchVersion}"
    }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'elasticsearch.esplugin'

group 'kr.co.wikibook'
version '1.0.0.0' + '-es-' + elasticsearchVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.elasticsearch:elasticsearch:${elasticsearchVersion}"
}

esplugin {
    name pluginName
    description pluginDescription
    classname pluginClassname
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnit()

    systemProperty("java.security.policy", "${projectDir}/src/test/resources/test-plugin-security.policy")

    System.getProperties().forEach({ key, value ->
        if (key instanceof String && key.startsWith("tests.")) {
            systemProperty(key, value)
        }
    })
}